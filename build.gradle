plugins {
    id 'java'
    id 'application'
    id 'com.apollographql.apollo' version "$apollo"
    id 'com.github.johnrengelman.shadow' version '7.+'
}

repositories {
    mavenCentral()
    maven {
        url = 'https://libraries.minecraft.net'
    }
    maven {
        url = 'https://m2.chew.pro/releases'
    }
}

java.toolchain {
    languageVersion = JavaLanguageVersion.of(21)
}

application {
    mainClass = 'net.neoforged.automation.Main'
}

run {
    workingDir(file('run'))
}

dependencies {
    implementation("io.javalin:javalin:6.2.0")
    implementation("org.slf4j:slf4j-simple:2.0.12")

    compileOnly 'org.jetbrains:annotations:23.1.0'
    implementation 'org.kohsuke:github-api:1.323'
    implementation 'org.bouncycastle:bcpkix-jdk15on:1.58'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.13.3'

    final jjwt = '0.10.5'
    implementation "io.jsonwebtoken:jjwt-api:$jjwt"
    implementation "io.jsonwebtoken:jjwt-impl:$jjwt"
    implementation "io.jsonwebtoken:jjwt-jackson:$jjwt"

    implementation 'com.mojang:brigadier:1.0.18'

    implementation "com.apollographql.apollo:apollo-api:$apollo" // Apollo (GraphQL)

    implementation 'org.eclipse.jgit:org.eclipse.jgit:6.10.0.202406032230-r'

    implementation('org.jdbi:jdbi3-core:3.32.0')
    implementation('org.jdbi:jdbi3-sqlobject:3.32.0')
    implementation('org.xerial:sqlite-jdbc:3.40.0.0')
    implementation('org.flywaydb:flyway-core:8.5.13')

    implementation 'net.dv8tion:JDA:5.4.0'
    implementation 'pw.chew:jda-chewtils:2.0'
}

apollo {
    service('service') {
        packageName = 'com.github.api'
        generateKotlinModels = false
        generatePrimitiveTypes = true
        languageVersion = '17'
        addTypename = 'ifPolymorphic'
        classesForEnumsMatching.set([])
        mapScalar('URI', 'java.net.URI')
    }
    generateSourcesDuringGradleSync.set(true)
}

shadowJar {
    mergeServiceFiles()
    archiveClassifier = 'all'
    archiveFile.set(project.file("$buildDir/libs/reactionable-all.jar"))
    manifest {
        attributes(['Multi-Release': true])
    }
}
